#!/usr/bin/perl

use strict;
use warnings;
use Getopt::Long qw(:config no_ignore_case);
use List::MoreUtils qw( none );

sub print_usage {
	print <<'_END_HELP';
$0 [-l | --length LENGTH] [-n | --no-special] [-r | --repeat NUMBER] [-e | --readable]
_END_HELP
	exit 0;
}

our $VERSION = '0.04';

srand;

my $EMPTY              = q{};
my $length             = 15;
my @chars              = ('A'..'Z', 'a'..'z', '0'..'9');
my @unreadable_chars   = split $EMPTY, q{oO0l1I};
my @special_chars      = split $EMPTY, q{!@#$%^&*()};
my $special            = 1;
my $repeat             = 10;
my $readable           = 0;

GetOptions(
        'l|length=i'	=> \$length,
        'r|repeat=i'	=> \$repeat,
        'n|no-special'	=> sub { $special = 0; },
        'e|readable'	=> \$readable,
        'h|help'	=> sub { print_usage(); },
) or exit 2;

if ($special) { push @chars, @special_chars; }
if ($readable) {
	@chars = grep {
		    $a = $_;
		    none { $a eq $_ } @unreadable_chars;
		  } @chars;
}

#use Data::Dumper; print Dumper(\@chars); exit;

for (1 .. $repeat) {
	my $password = $EMPTY;
	for (0 .. $length) {
		$password .= $chars[int rand @chars];
	}
	print "$password\n";
}

__END__

#################### main pod documentation begin ###################
=head1 NAME

genpass - Quickly create secure passwords

=head1 SYNOPSIS

genpass [-l | --length LENGTH] [-n | --no-special] [-r | --repeat NUMBER]

    -l | --length          password length
    -r | --repeat NUMBER   NUMBER of passwords to output
    -n | --no-special      do NOT include special characters: '!','@','#','$','%','^','&','*','(',')' 
    -e | --readable        print only easily readable characters (no "o", "O", "0", "l", "1", "I", "t")
    -h | --help            print a small usage line

=head1 DESCRIPTION

There are many jobs in which you just need to create a fast and secure password.
Sometimes you need one without special characters and sometimes you need to have a minimum length as well.
This script makes it possible, quickly and easily.

=head1 BUGS

None that I know of. Please report if and when you find any.

=head1 SUPPORT

If you have any problems or questions, contact me using the details below.

=head1 AUTHOR

    Sawyer X
    CPAN ID: XSAWYERX
    xsawyerx@cpan.org

=head1 COPYRIGHT

This program is free software; you can redistribute
it and/or modify it under the same terms as Perl itself.

The full text of the license can be found in the
LICENSE file included with this module.

=head1 A Word on Moral

Our lives depend on the decisions of others in the world, and so, other lives depend on our decisions.
When we decide to consume animals, we dedicate the death of others, and it is something to consider giving up.
Please review http://www.meatstinks.com and http://www.milksucks.com .
Thank you.

=head1 SEE ALSO

perl(1).

=cut

#################### main pod documentation end ###################

